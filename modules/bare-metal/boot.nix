{
  config,
  pkgs,
  lib,
  ...
}:
{

  boot = {
    #kernelPackages = pkgs.linuxPackages;

    # This pulls in nixos-containers which depends on Perl.
    enableContainers = false;

    tmp.cleanOnBoot = lib.mkDefault true;

    loader = {
      timeout = lib.mkDefault 0;
      generationsDir.copyKernels = true;

      systemd-boot = {
        enable = lib.mkDefault true;
        configurationLimit = lib.mkDefault 5;
      };

      grub = {
        enable = lib.mkDefault false;
        configurationLimit = lib.mkDefault 5;
        efiSupport = true;
        efiInstallAsRemovable = true;
        copyKernels = true;
        # Being headless, we don't need a GRUB splash image.
        splashImage = null;
        memtest86.enable = lib.mkForce false;
      };
    };

    # Make the installer more likely to succeed in low memory
    # environments.  The kernel's overcommit heustistics bite us
    # fairly often, preventing processes such as nix-worker or
    # download-using-manifests.pl from forking even if there is
    # plenty of free memory.
    kernel.sysctl."vm.overcommit_memory" = lib.mkDefault "1";

    initrd = {
      # Verbosity of the initrd
      # disabling verbosity removes only the mandatory messages generated by the NixOS
      verbose = false;
      systemd = {
        suppressedUnits = lib.mkIf config.systemd.enableEmergencyMode [
          "emergency.service"
          "emergency.target"
        ];
      };
    };

    kernelParams = [

      # Since we can't manually respond to a panic, just reboot.
      "panic=1"
      "boot.panic_on_fail"

      # https://en.wikipedia.org/wiki/Kernel_page-table_isolation
      # auto means kernel will automatically decide the pti state
      "pti=auto" # on | off

      # disable displaying of the built-in Linux logo
      "logo.nologo"

      "nohibernate"
    ];
  };
}
